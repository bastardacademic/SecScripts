// PART 1 -  run this part first! ----------------------------------------------------------------------
.set stored_query_result DeviceSuccessLogonEvents with (previewCount = 1000) <|
DeviceLogonEvents
| where ActionType == "LogonSuccess"
| distinct  DeviceName, AccountDomain, AccountName, LogonType, AccountSid
| join kind=inner (
    DeviceInfo
    | project DeviceId, DeviceName, OSPlatform, OnboardingStatus
    | where OnboardingStatus == "Onboarded"
) on DeviceName
| summarize by DeviceId, DeviceName, AccountDomain, AccountName, LogonType, OSPlatform
| order by DeviceId desc
| project Num=row_number(), DeviceId, DeviceName, AccountDomain, AccountName, LogonType, OSPlatform;
// ------------------------------------------------------------------------------------------------------
 
 
// PART 2 -  run this part as many times as you need to! ------------------------------------------------
let rowLimit = 1000;  // <--  set how many rows to return per page
let pageNumber = 1;   // <--  set what page to display for view/export
let startRow = rowLimit * (pageNumber - 1);
let endRow = rowLimit * pageNumber;
stored_query_result("DeviceSuccessLogonEvents")
| where Num between (startRow .. endRow)
// ------------------------------------------------------------------------------------------------------
 
//  Run this before re-running Part one, to clear the stored results  (unhash it first!).
//              
//               ||
//              _||_
//              \  /
//               \/
//              
// .drop stored_query_result DeviceSuccessLogonEvents
